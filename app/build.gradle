import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'io.github.ryanhoo.music'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode("$BUILD")
        versionName "$VERSION"
        vectorDrawables.useSupportLibrary = true
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared"
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }
    // If you want Gradle to package prebuilt native libraries
    // with your APK, modify the default source set configuration
    // to include the directory of your prebuilt .so files as follows.
    sourceSets {
        main {
            jniLibs.srcDirs 'libnode/bin/'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    api 'com.android.support:appcompat-v7:28.0.0'
    api 'com.android.support:design:28.0.0'
    api 'com.android.support:recyclerview-v7:28.0.0'
    api 'com.android.support:customtabs:28.0.0'
    api 'io.reactivex:rxjava:1.1.9'
    api 'io.reactivex:rxandroid:1.2.1'
    api 'com.jakewharton:butterknife:8.4.0'
    api 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.google.code.gson:gson:2.8.5'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    api 'uk.co.chrisjenx:calligraphy:2.2.0'
    api 'com.squareup.okio:okio:2.1.0'
}

def getVersionCode(build) {
    String date = new SimpleDateFormat("yyyyMMdd", Locale.getDefault()).format(new Date())
    return Integer.parseInt(date + build)
}
